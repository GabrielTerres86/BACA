DECLARE
    V_LIMIT NUMBER := 5000;
  
    CURSOR CR_EPR_TO_UPDATE IS
      SELECT EPR.ROWID ROWIDEPR, LCR.TXMENSAL
        FROM CRAPEPR EPR, CRAPLCR LCR, CRAPCOP COP
       WHERE EPR.CDCOOPER = LCR.CDCOOPER
         AND EPR.CDLCREMP = LCR.CDLCREMP
         AND EPR.CDCOOPER = COP.CDCOOPER
         AND LCR.CDCOOPER = COP.CDCOOPER
         AND COP.FLGATIVO = 1
         AND EPR.TPEMPRST = 0 -- TR
         AND EPR.INLIQUID = 1 -- LIQUIDADO
         AND EPR.INPREJUZ = 1 -- EM PREJUIZO
         AND NVL(EPR.TXMENSAL, 0) = 0 -- TAXA MENSAL
         AND EPR.VLSDPREJ > 0; -- SALDO PREJUIZO
    TYPE T_EPR_TO_UPDATE IS TABLE OF CR_EPR_TO_UPDATE%ROWTYPE INDEX BY PLS_INTEGER;
    L_EPR_TO_UPDATE T_EPR_TO_UPDATE;
  
  BEGIN
  
    DBMS_OUTPUT.PUT_LINE('INI: ' || TO_CHAR(SYSDATE, 'DD/MM/RRRR HH24:MI:SS'));
  
    OPEN CR_EPR_TO_UPDATE;
    LOOP
      FETCH CR_EPR_TO_UPDATE BULK COLLECT
        INTO L_EPR_TO_UPDATE LIMIT V_LIMIT;
    
      EXIT WHEN L_EPR_TO_UPDATE.COUNT = 0;
    
      FORALL IDX IN 1 .. L_EPR_TO_UPDATE.COUNT SAVE EXCEPTIONS
        UPDATE CECRED.CRAPEPR
           SET TXMENSAL = L_EPR_TO_UPDATE(IDX).TXMENSAL
         WHERE ROWID = L_EPR_TO_UPDATE(IDX).ROWIDEPR;
    
      DBMS_OUTPUT.PUT_LINE(SQL%ROWCOUNT || ' registros atualizados.');
    
    END LOOP;

    COMMIT;
  
    DBMS_OUTPUT.PUT_LINE('FIM: ' || TO_CHAR(SYSDATE, 'DD/MM/RRRR HH24:MI:SS'));
  
    IF (CR_EPR_TO_UPDATE%ISOPEN) THEN
      CLOSE CR_EPR_TO_UPDATE;
    END IF;
  
  EXCEPTION
    WHEN OTHERS THEN
      FOR IDX IN 1 .. SQL%BULK_EXCEPTIONS.COUNT LOOP
        DBMS_OUTPUT.put_line(SQL%BULK_EXCEPTIONS(IDX).ERROR_INDEX || ': ' || SQL%BULK_EXCEPTIONS(IDX).ERROR_CODE);
      END LOOP;
  END;
