<!--
  Programa : crap018.htm
  Autor    : Guilherme/Elton
  Data     : Julho / 2010
         
                                               Última Alteração: 31/07/2015
                                                  
  Descrição: Gerar arquivo parcial de truncagem de cheques.
  
  Alterações: 14/01/2011 - Alterado para buscar cheques que pertencem ao 
                           lote 11000 e banco/caixa 11 ou lote 28000 e 
                           banco/caixa 500 (Elton).
             
             28/01/2011 - Comentado banco/caixa 500, para nao digitalizar
                          cheques lancados pela LANCHQ (Elton).
                          
             17/05/2011 - Descomentado banco/caixa 500 (Elton).
             
             22/09/2011 - Incluir lote 30000 ref. rotina 66 (Guilherme).
             
             06/10/2011 - Incluido tratamento para Concredi para que cheques da 
                          propria cooperativa sejam digitalizados (Elton).
                          
             13/02/2012 - Alteracao para que todas as coops possam digitalizar
                          cheques da propria cooperativa (ZE).
                          
             25/04/2013 - Ajuste na chamada da procedure 
                          reativar arquivos cecred para tratamento de erro
                          (David Kruger).
                          
             05/09/2013 - Nova forma de chamar as agências, de PAC agora 
                            a escrita será PA (André Euzébio - Supero).             
                            
             25/09/2014 - Ajustado problema ao gerar prévia dos cheques. 
                          (Reinert)
                          
             31/07/2015 - Ajuste para retiar o caminho absoluto na chamada
                          de fontes
                          (Adriano - SD 314469).   
              
             12/09/2016 - Adicionado chamada da função "callBlass(event)" - (Evandro - RKAM).                         
			 
			 17/05/2018 - Utilização do caixa on-line mesmo com o processo batch (noturno) executando
			              (Fabio Adriano - AMcom).
.............................................................................-->

<html>

<head>
<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<meta name="GENERATOR" content="Microsoft FrontPage 4.0">
<meta name="ProgId" content="FrontPage.Editor.Document">
<meta http-equiv="cache-control" content="no-cache">
<meta http-equiv="Pragma"        content="No-Cache">
<meta http-equiv="Expires"       content="0">
<script language=JavaScript src="/script/formatadadosie.js"></script>
<link rel=StyleSheet type="text/css" href="/script/viacredi.css">
<title>TRUNCAGEM CHEQUES - PARCIAL</title>

</head>

<body background="/images/moeda.jpg" bgproperties="fixed" class=fundo onLoad="JavaScript:mudaFoco();" onKeyUp="callBlass(event); callBLini(event); callBLsal(event);">

<OBJECT  classid="clsid:7F8735B1-EC41-4134-9083-E059B3F56262" codebase="edimpbmp20ci.ocx#version=2,1,3,1" width=0 height=0 align=CENTER hspace=0 vspace=0 id=bematech>
   </OBJECT>

<form onKeyDown="change_page(event)" method=post> 
<input type="hidden" name="vh_foco" value="1">                      
<input type="hidden" name="v_btn_gerar" value="false">

<script language="SpeedScript">
    
    DEF VAR v_coop        AS CHAR NO-UNDO.
    DEF VAR v_pac         AS CHAR NO-UNDO.
    DEF VAR v_caixa       AS CHAR NO-UNDO.
    DEF VAR v_operador    AS CHAR NO-UNDO.
    DEF VAR v_data        AS CHAR NO-UNDO.
    DEF VAR v_reativa     AS CHAR NO-UNDO.
    
    DEF VAR aux_nrdlote1  AS INTE NO-UNDO.
    DEF VAR aux_nrdlote2  AS INTE NO-UNDO.  
    DEF VAR aux_nrdlote3  AS INTE NO-UNDO.  
    DEF VAR aux_cdbccxlt  AS CHAR NO-UNDO. 
    
    DEF VAR l-houve-erro  AS LOG  NO-UNDO.
    
    DEF VAR aux_qtcheque  AS INTE NO-UNDO.
    DEF VAR aux_vlcheque  AS DECI NO-UNDO.
    DEF VAR aux_dssitprv  AS CHAR NO-UNDO.
    DEF VAR aux_contador  AS INTE NO-UNDO.
    DEF VAR aux_visuaprv  AS CHAR NO-UNDO.
    DEF VAR aux_emiteprv  AS CHAR NO-UNDO.
    DEF VAR aux_reatiprv  AS CHAR NO-UNDO.
   
    DEF VAR aux_cdcritic  AS INTE NO-UNDO.
    DEF VAR aux_qtarquiv  AS INTE NO-UNDO.
    DEF VAR aux_totregis  AS INTE NO-UNDO.
    DEF VAR aux_vlrtotal  AS DECI NO-UNDO.

    DEF VAR aux_nmarquiv  AS CHAR NO-UNDO.
    DEF VAR aux_dslitera  AS CHAR NO-UNDO.
    DEF VAR vh_arquivo    AS CHAR NO-UNDO.
    
    DEF VAR h-b1wgen0012 AS HANDLE.

    DEFINE TEMP-TABLE tt-previa NO-UNDO
        FIELD nrprevia  AS   INTE  
        FIELD hrprevia  AS   CHAR
        FIELD qtcheque  AS   INTE
        FIELD vlcheque  AS   DECI
        FIELD insitprv  AS   INTE
        FIELD dssitprv  AS   CHAR.
    
    { dbo/bo-erro1.i }
    { sistema/generico/includes/b1wgen0012tt.i }
    { sistema/generico/includes/var_internet.i }
          
    /* Criada para montar o JavaScript nao ficar com o código feio */
    PROCEDURE JavaScript:

        {include/monta_js.i} /* Utilizada includes pois o programa não compila*/
    
    END PROCEDURE. 


    FIND crapcop WHERE crapcop.nmrescop = GET-VALUE("user_coop") NO-LOCK NO-ERROR.

    FIND crapdat WHERE crapdat.cdcooper = crapcop.cdcooper  NO-LOCK NO-ERROR.

    ASSIGN v_caixa      = GET-VALUE("User_cx")
           v_operador   = GET-VALUE("operador")
           v_pac        = GET-VALUE("User_pac")
           v_coop       = GET-VALUE("user_coop")
           v_data       = (STRING(crapdat.dtmvtocd,"99/99/9999")) 
           v_reativa    = GET-VALUE("preativa")
           aux_nrdlote1 = 11000 + INT(v_caixa)
           aux_nrdlote2 = 28000 + INT(v_caixa)
           aux_nrdlote3 = 30000 + INT(v_caixa)
           aux_cdbccxlt = "11,500". 

    RUN elimina-erro (INPUT crapcop.nmrescop,  
                      INPUT INT(v_pac),
                      INPUT INT(v_caixa)).
    
    EMPTY TEMP-TABLE tt-previa.

    FOR EACH crapchd WHERE crapchd.cdcooper = crapcop.cdcooper      AND
                           crapchd.cdagenci = INT(v_pac)            AND
                           crapchd.cdoperad = v_operador            AND
                           crapchd.dtmvtolt = crapdat.dtmvtocd AND
                           CAN-DO(aux_cdbccxlt, STRING(crapchd.cdbccxlt))   AND
                          (crapchd.nrdolote = aux_nrdlote1          OR 
                           crapchd.nrdolote = aux_nrdlote2          OR
                           crapchd.nrdolote = aux_nrdlote3)         NO-LOCK
                           BREAK BY crapchd.nrprevia:

        ASSIGN  aux_qtcheque = aux_qtcheque + 1
                aux_vlcheque = aux_vlcheque + crapchd.vlcheque.
        
        IF  LAST-OF(crapchd.nrprevia) THEN
            DO:
                CASE (crapchd.insitprv) :
                    WHEN 0 THEN
                        aux_dssitprv = "Nao Enviado".
                    WHEN 1 THEN
                        aux_dssitprv = "Gerado".
                    WHEN 2  THEN
                        aux_dssitprv = "Digitalizado".
                    WHEN 3 THEN
                        aux_dssitprv = "Processado".
                    WHEN 4 THEN
                        aux_dssitprv = "Pendente".
                    OTHERWISE
                        aux_dssitprv = "Inválida".
                END CASE.
    
                CREATE  tt-previa.
                ASSIGN  tt-previa.nrprevia = crapchd.nrprevia
                        tt-previa.hrprevia = STRING(crapchd.hrprevia,"HH:MM:SS")
                        tt-previa.qtcheque = aux_qtcheque
                        tt-previa.vlcheque = aux_vlcheque
                        tt-previa.insitprv = crapchd.insitprv
                        tt-previa.dssitprv = aux_dssitprv.

                ASSIGN  aux_qtcheque = 0
                        aux_vlcheque = 0
                        aux_dssitprv = "".
            END.
    END.

    IF  REQUEST_METHOD = "POST":U  THEN 
        DO:             
            IF  GET-VALUE("v_btn_gerar") = 'true' THEN  
                DO:
                    ASSIGN l-houve-erro = NO.

                    /*** Verifica se PA pode gerar previa ***/
                    FIND craptab WHERE   craptab.cdcooper = crapcop.cdcooper   AND
                                         craptab.nmsistem = "CRED"         AND
                                         craptab.tptabela = "GENERI"       AND
                                         craptab.cdempres = 0              AND
                                         craptab.cdacesso = "EXETRUNCAGEM" AND
                                         craptab.tpregist = INT(v_pac)     NO-LOCK NO-ERROR.
                          
                    IF  (NOT AVAIL craptab         OR 
                         craptab.dstextab = "NAO") THEN 
                         DO:
                            </script>
                                <script language="JavaScript">
        
                                    alert("PA nao executa previa.");
        
                                </script>
                            <script language="SpeedScript">
                         END.
                    ELSE
                        DO:
                            RUN sistema/generico/procedures/b1wgen0012.p PERSISTENT SET h-b1wgen0012.
                                           
                            RUN gerar_arquivos_cecred IN h-b1wgen0012 (INPUT "COMPEL",
                                                                       INPUT crapdat.dtmvtocd,
                                                                       INPUT crapcop.cdcooper, 
                                                                       INPUT INTE(v_pac), /* Ini */
                                                                       INPUT INTE(v_pac), /* Fim */
                                                                       INPUT v_operador,
                                                                       INPUT "ROTINA18",
                                                                       INPUT "", 
                                                                       INPUT  INTE(v_caixa), 
                                                                       INPUT  aux_cdbccxlt, 
                                                                       OUTPUT aux_cdcritic,
                                                                       OUTPUT aux_qtarquiv,
                                                                       OUTPUT aux_totregis,
                                                                       OUTPUT aux_vlrtotal).
                           
                            DELETE PROCEDURE h-b1wgen0012.
                                            
                            IF  RETURN-VALUE = "NOK"  THEN
                                DO:          
                                    RUN cria-erro (INPUT crapcop.nmrescop,
                                                   INPUT INTE(v_pac),
                                                   INPUT INT(v_caixa),  
                                                   INPUT aux_cdcritic,
                                                   INPUT "",
                                                   INPUT YES).
                                    
                                    RUN verifica-erro (INPUT crapcop.nmrescop,
                                                       INPUT INTE(v_pac),
                                                       INPUT INT(v_caixa)).
                                                       
                                    IF  AVAIL craperr  THEN                  
                                        RUN JavaScript("window.open('mensagem.p','werro','height=220,width=400,scrollbars=yes,alwaysRaised=true')").
                                END.
                            ELSE
                                DO: 
                                   RUN JavaScript("window.open('crap018.w','_self')").
                                END.

                        END. /* final do ELSE AVAIL craptab*/
                    
                END. /* Final do GET VALUE gerar <> ""  */  
            ELSE
                DO:
                    FOR EACH tt-previa NO-LOCK:

                        ASSIGN  aux_visuaprv = "visualiza" + STRING(tt-previa.nrprevia)
                                aux_emiteprv = "emite"     + STRING(tt-previa.nrprevia)
                                aux_reatiprv = "reativa"   + STRING(tt-previa.nrprevia).
                                                
                        IF  GET-VALUE(aux_visuaprv) <> "" THEN
                            DO:    
                                RUN sistema/generico/procedures/b1wgen0012.p PERSISTENT SET h-b1wgen0012.
                                
                                RUN visualiza_cheques_previa IN h-b1wgen0012 (INPUT v_coop,
                                                                              INPUT v_operador,
                                                                              INPUT INT(v_pac),
                                                                              INPUT INT(v_caixa),
                                                                              INPUT aux_cdbccxlt, 
                                                                              INPUT tt-previa.nrprevia, 
                                                                              OUTPUT aux_nmarquiv).
                          
                                DELETE PROCEDURE h-b1wgen0012.     
                                
                                ASSIGN vh_arquivo = HostURL + "/" + aux_nmarquiv.

                                </script>
                                <script language="JavaScript">

                                        window.open('`vh_arquivo`','wvisual','width=835,height=350,scrollbars=yes,content=no-cache,titlebar=Titulos,alwaysRaised=true,left=0,top=0');

                                </script>
                                <script language="SpeedScript">
                                
                            END. 
                        ELSE
                        IF  GET-VALUE(aux_emiteprv) <> "" THEN
                            DO:     
                                RUN sistema/generico/procedures/b1wgen0012.p PERSISTENT SET h-b1wgen0012.
            
                                RUN emite_protocolo_previa IN h-b1wgen0012 (INPUT v_coop,
                                                                            INPUT v_data, 
                                                                            INPUT v_operador,
                                                                            INPUT INT(v_pac),
                                                                            INPUT INT(v_caixa),
                                                                            INPUT tt-previa.nrprevia,
                                                                            INPUT tt-previa.hrprevia,
                                                                            INPUT tt-previa.qtcheque,
                                                                            INPUT tt-previa.vlcheque,
                                                                            OUTPUT aux_dslitera).
                                DELETE PROCEDURE h-b1wgen0012.

                                </script>
                                <script language="JavaScript">
                                        
                                        bematech.ImpAjust("LPT1", "`aux_dslitera`" ,48,1,2,0,0,0,0);

                                </script>  
                                <script language="SpeedScript">

                            END.
                        ELSE
                        IF  GET-VALUE(aux_reatiprv) <> "" THEN
                            DO:     

                                  RUN JavaScript("window.open('logingerente.w?v_prog=crap018.htm&preativa=" + STRING(tt-previa.nrprevia) + "','_self')").
                                  
                            END.
                        
                    END.  /** DO aux_contador **/
                END.
        END.
    ELSE /* GET */
        DO: 
            IF  v_reativa <> "" THEN
                DO: 
                    RUN sistema/generico/procedures/b1wgen0012.p PERSISTENT SET h-b1wgen0012.
                       
                    RUN reativar_arquivos_cecred IN h-b1wgen0012 (INPUT  "COMPEL",
                                                                  INPUT  crapcop.cdcooper,
                                                                  INPUT  crapdat.dtmvtocd,
                                                                  INPUT  INT(v_pac),
                                                                  INPUT  FALSE, 
                                                                  INPUT  0,             
                                                                  INPUT  "ROTINA18",    
                                                                  INPUT  v_reativa,    
                                                                  INPUT  INT(v_caixa), 
                                                                  INPUT  aux_cdbccxlt,
																  INPUT  3,
                                                                  OUTPUT aux_cdcritic). 
                    DELETE PROCEDURE h-b1wgen0012.

                    IF  RETURN-VALUE <> "OK"  THEN
                        DO:          
                            RUN cria-erro (INPUT crapcop.nmrescop,
                                           INPUT INTE(v_pac),
                                           INPUT INT(v_caixa),  
                                           INPUT aux_cdcritic,
                                           INPUT "",
                                           INPUT YES).
                               
                            RUN verifica-erro (INPUT crapcop.nmrescop,
                                               INPUT INTE(v_pac),
                                               INPUT INT(v_caixa)).
                                                  
                            IF  AVAIL craperr  THEN                  
                                RUN JavaScript("window.open('mensagem.p','werro','height=220,width=400,scrollbars=yes,alwaysRaised=true')").
                        END.
                     ELSE
                        RUN JavaScript("window.location='crap018.htm'").
    
                END.           
        END.

</script>



<p style="word-spacing: 0; line-height: 100%; margin-top: 0; margin-bottom: 0">&nbsp;</p>

<div align="center">
  <center>
  <table width="100%" class=cabtab>
    <tr>
       <td width="15%" align="center"> <input type="text" name="v_coop" value="`v_coop`" size="11" class="transp" readOnly>
      </td>
      <td width="20%" align="center"> PA&nbsp; <input type="text" name="v_pac" value="`v_pac`" size="1" class="transp"  readOnly>
      </td>
      <td width="20%" align="center"> CAIXA&nbsp; <input type="text" name="v_caixa" value="`v_caixa`" size="1" class="transp" readOnly>
      </td>
      <td width="30%" align="center"> OPERADOR&nbsp; <input type="text" name="v_operador" value="`v_operador`" size="7" class="transp" readOnly>
      </td>
      <td width="15%" align="center"> <input type="text" name="v_data" value="`v_data`" size="10" class="transp" readOnly>
      </td>
    </tr>
  </table>
  </center>
</div>
<div align="center">
  <center>
  <table width="100%" class=cabtab>
    <tr>
      <td class=titulo>TRUNCAGEM DE CHEQUES - PARCIAL</td>
      <td width="21%" class=programa align="right">P.018&nbsp; V.1.00</td>
    </tr>
  </table>
  </center>
</div>

<div align="center">
  <center>
    <table cellspacing = 0 cellpadding= 0 width=100%>
      <tr>
        <td width="100%" valign="middle" align="center">
          <div align="center">
            <center>
                <table width="101%" cellspacing = 0 cellpadding = 0 class=tcampo >
                    <tr>
                      <td class="linhaform">&nbsp;</td>
                    </tr>
                    <tr>
                      <td class="linhaform">&nbsp;</td>
                    </tr>
                    <tr>
                      <td>
                        <table class="tabela">
                          <tr>     
                            <td>
                              <table border="1" cellspacing = 0 cellpadding = 5 style="font-family:Arial;font-size:10px" width="100%" class=dadostab>
                                <tr style="background-color:Green;color:LightGoldenRodYellow;font-size:10px;height=35">  
                                    <td align="center"> &nbsp;&nbsp; <b>  Prévia                 </b> &nbsp;&nbsp;  </td>
                                    <td align="center"> &nbsp;&nbsp; <b>  Horário                </b> &nbsp;&nbsp;  </td>
                                    <td align="center"> &nbsp;&nbsp; <b>  Qtd. Cheques           </b> &nbsp;&nbsp;  </td>
                                    <td align="center"> &nbsp;&nbsp; <b>  Valor                  </b> &nbsp;&nbsp;  </td>
                                    <td align="center"> &nbsp;&nbsp; <b>  Situação               </b> &nbsp;&nbsp;  </td>
                                </tr>
                                <script language="SpeedScript">
                                        FOR EACH tt-previa NO-LOCK BY tt-previa.insitprv DESC 
                                                                        BY tt-previa.nrprevia :
                                </script>
                                <tr style="height=35">
                                    <script language="SpeedScript">
                                            IF tt-previa.insitprv = 0 THEN DO:
                                    </script>
                                    <td align="center"> &nbsp; </td>
                                    <td align="center"> &nbsp; </td>
                                    <script language="SpeedScript">
                                            END. ELSE DO:
                                    </script>
                                    <td align="center">`STRING(tt-previa.nrprevia,"999")` </td>
                                    <td align="center" >`tt-previa.hrprevia` </td>
                                    <script language="SpeedScript">
                                            END.
                                    </script>
                                    <td align="center" > `STRING(tt-previa.qtcheque,"zzz,999")` </td>
                                    <td align="right" > &nbsp;&nbsp; `STRING(tt-previa.vlcheque,"zzz,zzz,zz9.99")` &nbsp;&nbsp; </td>
                                    <td align="center"> `tt-previa.dssitprv` </td>
                                </tr>
                                <script language="SpeedScript">
                                        END.
                                </script>
                              </table> 
                              
                            </td> 
                            <td>   
                              <table class=tbbotoes cellspacing = 0 cellpadding = 5>
                                <tr style="height=35">
                                    <td> <br> </td>
                                </tr>
                                <script language="SpeedScript">
                                FOR EACH tt-previa NO-LOCK BY  tt-previa.insitprv DESC 
                                                               BY tt-previa.nrprevia:
                                </script>
                                <tr style="height=35">
                                    <script language="SpeedScript">
                                        IF tt-previa.insitprv <> 0 THEN DO: 
                                    </script>
                                        <td valign="middle" align="center"><input type="submit" value="Visualizar Cheques" name="visualiza`STRING(tt-previa.nrprevia)`" class="button" style="width=100;height=25;font-size:9px"></td>
                                        <td valign="middle" align="center"><input type="submit" value="Emitir Protocolo"   name="emite`STRING(tt-previa.nrprevia)`"     class="button" style="width=80;height=25;font-size:9px" ></td>

                                    <script language="SpeedScript">
                                            IF tt-previa.insitprv = 1 THEN DO:
                                             </script>
                                               <td valign="middle" align="center"><input type="submit" value="Reativar Cheques"   name="reativa`STRING(tt-previa.nrprevia)`"   class="button" style="width=90;height=25;font-size:9px" ></td>
                                    <script language="SpeedScript"> 
                                            END.     
                                        END.
                                        ELSE DO:
                                    </script>
                                    <td> &nbsp; </td>
                                    <script language="SpeedScript">
                                    END.
                                    </script>
                                </tr>        
                                <script language="SpeedScript">
                                END.
                                </script>
                              </table>
                            </td>
                          </tr> 
                          <tr>
                             <td class="linhaform">&nbsp;</td>
                          </tr>
                          <tr>
                            <td valign="middle" align="center">  <input type="submit" value="Gerar Previa" name="gerar"  onClick="document.forms[0].gerar.disabled='true';document.forms[0].v_btn_gerar.value='true'; submit(); " class="button" style="width=80;height=25;font-size:9px"> </td>
                          </tr>
                        </table>
                      </td>
                    </tr>
                    <tr>
                       <td class="linhaform">&nbsp;</td>
                    </tr>
                </table>
             </center>
          </div>
        </td>
      </tr>
    </table>
  </center>
</div>  
</form>



</body>
</html>


